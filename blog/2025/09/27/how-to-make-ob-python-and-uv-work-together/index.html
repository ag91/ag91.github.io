<!DOCTYPE html>
<html lang="en-us">
  <head>
    <title>How to make ob-python and UV work together - Where parallels cross</title>
    <meta charset="utf-8" />
    <meta name="author" content="Andrea" />
    <meta name="keywords" content="&lt;TODO: insert your keywords here&gt;" />
    <link rel="stylesheet" href="/media/css/main.css" type="text/css">
    <link rel="stylesheet" href="/media/css/prettify.css" type="text/css">
  </head>
  <body class="container">
    <div>
      <header class="masthead">
        <h1 class="masthead-title"><a href="/">Where parallels cross</a></h1>
        <p>Interesting bits of life</p>
        <ul>
          <li><a href="/blog/">Blog</a></li>
          <li><a href="/offering/">Offering</a></li>
          <li><a href="/tags/">Tags</a></li>
          <li><a href="/about/">About</a></li>
          <li><a href="https://github.com/ag91">GitHub</a></li>
          <li><a href="/rss.xml">RSS</a></li>
        </ul>
        <form method="get" id="searchform" action="//www.google.com/search">
          <input type="text" class="field" name="q" id="s" placeholder="Search">
          <input type="hidden" name="as_sitesearch" value="ag91.github.io">
        </form>
      </header>
    </div>

<div>
<div class="post">
<h1>How to make ob-python and UV work together</h1>
<p>
<b>EDIT:</b> I discovered there is a better way (<a href="https://alexguerard.com/blog/org-and-uv">here the description</a>):
just do
</p>

<div class="org-src-container">
<pre class="src src-org">#+begin_src python :results output :python uv run --env-file .env -
</pre>
</div>





<p>
It is been a while! I have been very busy between work and family, but
I never stopped using Emacs.
</p>

<p>
So for work I ended up moving from Clojure to Scala to now Python.
Since <a href="https://docs.astral.sh/uv/">uv</a> is now the winning project manager for Python, I have been
using in my side projects.
</p>

<p>
As always I like to experiment via org babel and one of the things
that make me satisfied is to try a new library on the fly in an Org
Mode block.
</p>

<p>
I didn't seem to find that feature in ob-python.el (<a href="https://ag91.github.io/blog/2023/08/03/an-easier-way-to-try-out-clojure-libraries-with-ob-clojure-and-cider/">nor did I find in
ob-clojure to be honest</a>) so I just built it on the fly.
</p>

<p>
So <code>uv</code> comes with scripts that can load dependencies, and this is the org block I wanted to run:
</p>

<div class="org-src-container">
<pre class="src src-org">#+begin_src python :uv nil :results output
# /// script
# dependencies = [
#   "pydantic",
#   "hypothesis-jsonschema",
# ]
# ///

from hypothesis import given
from hypothesis_jsonschema import from_schema
from pydantic import BaseModel

class Item(BaseModel):
    name: str
    qty: int = 1

schema = Item.model_json_schema()
strategy = from_schema(schema)

@given(strategy)
def test_item(data):
    item = Item.model_validate(data)
    assert item.qty &gt;= 0

# B. Manual strategy for fields (more control)

from hypothesis import given, strategies as st
from pydantic import BaseModel

class Item(BaseModel):
    name: str
    qty: int

name_s = st.text(min_size=1, max_size=50)
qty_s = st.integers(min_value=0, max_value=1000)

@given(st.builds(Item, name=name_s, qty=qty_s))
def test_item_manual(item):
    print(f"---- {item.qty}")
    assert item.qty &gt;= 0

print(qty_s.example())

test_item_manual()
#+end_src

</pre>
</div>


<p>
As you can see here I am learning about pydantic and
hypothesis-jsonschema, which together allow you to do property based testing in Python.
</p>

<p>
Below the code to make ob-python.el digest that block:
</p>

<div class="org-src-container">
<pre class="src src-elisp">(add-to-list 'org-babel-header-args:python
             '(uv . :any)
             )

(defcustom org-babel-python-uv-default-dependencies '()
  "Default packages to add when running Python via uv.
   These are appended to dependencies detected from the block body
   (uv script header) and any provided explicitly to :uv."
  :package-version '(Org . "9.7")
  :group 'org-babel
  :type '(repeat string))
(defun org-babel-python--uv-deps-from-body (body)
  "Parse uv script header in BODY and return a list of dependencies.
   Looks for a header starting with =# /// script= and ending at a
   line =# ///=, and collects all quoted strings within."
  (let ((deps nil))
    (save-match-data
      (when (string-match "^#\\s-*///\\s-*script" body)
        (let* ((start (match-end 0))
               (end (or (string-match "^#\\s-*///\\s-*$" body start)
                        (string-match "^#\\s-*///" body start)
                        (length body)))
               (header (substring body start end))
               (pos 0))
          (while (string-match "\\(['\"]\\)\\([^\"']+\\)\\1" header pos)
            (push (match-string 2 header) deps)
            (setq pos (match-end 0))))))
    (nreverse (delete-dups deps))))

(defun org-babel-python--uv-collect-deps (uv-param body)
  "Return list of dependencies for :uv header UV-PARAM and BODY."
  (let* ((explicit
          (cond
           ((listp uv-param) uv-param)
           (t nil)))
         (deps (append explicit
                       (org-babel-python--uv-deps-from-body body)
                       org-babel-python-uv-default-dependencies)))
    (message "hey-- %s " deps)
    (delete-dups deps)))
(defun org-babel-python--build-uv-command (deps)
  "Return an =uv run= command string using DEPS."
  (message "hey123-- %s " (concat
                           "uv run "
                           (mapconcat (lambda (d)
                                        (concat "--with " (shell-quote-argument d)))
                                      deps " ")
                           (when deps " ")
                           "python"))
  (concat
   "uv run "
   (mapconcat (lambda (d)
                (concat "--with " (shell-quote-argument d)))
              deps " ")
   (when deps " ")
   "python"))

(defun org-babel-execute:python (body params)
  "Execute Python BODY according to PARAMS.
   This function is called by =org-babel-execute-src-block'."
  (let* ((uv-param (cdr (assq :uv params)))
         (uv-cmd (when uv-param
                   (org-babel-python--build-uv-command
                    (org-babel-python--uv-collect-deps uv-param body))))
         (org-babel-python-command
          (or uv-cmd
              (cdr (assq :python params))
              org-babel-python-command))
         (session (org-babel-python-initiate-session
                   (cdr (assq :session params))))
         (graphics-file (and (member "graphics" (assq :result-params params))
                             (org-babel-graphical-output-file params)))
         (result-params (cdr (assq :result-params params)))
         (result-type (cdr (assq :result-type params)))
         (return-val (when (eq result-type 'value)
                       (cdr (assq :return params))))
         (preamble (cdr (assq :preamble params)))
         (async (org-babel-comint-use-async params))
         (full-body
          (concat
           (org-babel-expand-body:generic
            body params
            (org-babel-variable-assignments:python params))
           (when return-val
             (format (if session "\n%s" "\nreturn %s") return-val))))
         (result (org-babel-python-evaluate
                  session full-body result-type
                  result-params preamble async graphics-file)))
    (org-babel-reassemble-table
     result
     (org-babel-pick-name (cdr (assq :colname-names params))
                          (cdr (assq :colnames params)))
     (org-babel-pick-name (cdr (assq :rowname-names params))
                          (cdr (assq :rownames params))))))
</pre>
</div>

<p>
The idea is to extract the dependencies from the body of the block
with a regex and run <code>uv run --with &lt;dep&gt; --with &lt;dep1&gt; ... python</code>,
and reuse ob-python functionality for the rest.
</p>

<p>
I didn't test it much but it works.
</p>

<p>
Hopefully makes it easier and inspires others to try things out in
Python.
</p>

<p>
Happy hacking!
</p>

</div>
</div>
    <div>
      <div class="post-meta">
        <span title="post date" class="post-info">2025-09-27</span>
        <span title="last modification date" class="post-info">2025-09-27</span>
        <span title="tags" class="post-info"><a href="/tags/&lt;todo/">&lt;TODO</a>, <a href="/tags/insert-your-tags-here&gt;/">insert your tags here&gt;</a></span>
        <span title="author" class="post-info">Andrea</span>
      </div>
      <script src="//code.jquery.com/jquery-latest.min.js"></script>
      <script src="https://cdnjs.cloudflare.com/ajax/libs/prettify/r298/prettify.js"></script>
      <script src="https://cdn.mathjax.org/mathjax/latest/MathJax.js?config=TeX-AMS-MML_HTMLorMML"></script>
      <script src="/media/js/main.js"></script>
      <div class="footer">
        <p>Generated by <a href="http://www.gnu.org/software/emacs/">Emacs</a> 30.x (<a href="http://orgmode.org">Org mode</a> 9.x)</p>
        <p>
          Copyright &copy; 2012 - <span id="footerYear"></span> <a href="mailto:andrea-dev &lt;at&gt; hotmail &lt;dot&gt; com">Andrea</a>
          &nbsp;&nbsp;-&nbsp;&nbsp;
          Powered by <a href="https://github.com/kelvinh/org-page" target="_blank">org-page</a>
          <script type="text/javascript">document.getElementById("footerYear").innerHTML = (new Date()).getFullYear();</script>
        </p>
      </div>
    </div>

  </body>
</html>
